// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
	Not(in = instruction[15], out = notC);
	And(a = instruction[15], b = instruction[5], out = ldALU2A);

	Mux16(a = instruction, b = outALU, sel = ldALU2A, out = o1);

	Or(a = notC, b = instruction[5], out = ldA);
	ARegister(in = o1, load = ldA, out = outA, out[0..14] = addressM);

	Mux16(a = outA, b = inM, sel = instruction[12], out = o2);
	
	And(a = instruction[15], b = instruction[4], out = lhsD);
	DRegister(in = outALU, load = lhsD, out = outD);

	And(a = instruction[15], b = instruction[11], out = zx);
	And(a = instruction[15], b = instruction[10], out = nx);
	And(a = instruction[15], b = instruction[9], out = zy);
	And(a = instruction[15], b = instruction[8], out = ny);
	And(a = instruction[15], b = instruction[7], out = f);
	And(a = instruction[15], b = instruction[6], out = no);
	
	ALU(x = outD, y = o2, zx = zx, nx = nx, zy = zy, ny = ny, f = f, no = no, out = outALU, out = outM, zr = outZr, ng = outNg);
	And(a = instruction[15], b = instruction[3], out = writeM);
	 
	Or(a = outZr, b = outNg, out = notPos);
	Not(in = notPos, out = outPos);
	
	And(a = instruction[15], b = instruction[0], out = j0);
	And(a = instruction[15], b = instruction[1], out = j1);
	And(a = instruction[15], b = instruction[2], out = j2);

	And(a = j0, b = outPos, out = pos);
	And(a = j1, b = outZr, out = zero);
	And(a = j2, b = outNg, out = neg);
	Or(a = pos, b = zero, out = or1);
	Or(a = or1, b = neg, out = jmp);

	PC(in = outA, load = jmp, inc = true, reset = reset, out[0..14] = pc);
}
